apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: rootProject.file('dependencies.gradle')
apply from: rootProject.file('versions.gradle')
apply plugin: 'io.fabric'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId versions.appId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    androidExtensions {
        experimental = true
    }

}

afterEvaluate {
    initFabricPropertiesIfNeeded()
}

dependencies {
    implementation project(path: ':domain')
    implementation project(path: ':data')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation jUnit

    androidTestImplementation(testDependencies)

    kapt(librariesAnnotationProcessors)

    implementation(appDependencies)

    debugImplementation(toolsDependenciesDebug)

    releaseImplementation(toolsDependenciesRelease)

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
}

def initFabricPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: crashlyticsdemoApisecret)
            entry(key: "apiKey", value: crashlyticsdemoApikey)
        }
    }
}
